# Copyright (c) 2023 Microsoft Corporation
#
# SPDX-License-Identifier: Apache-2.0
#
package agent_policy

# Not required for rego v1. regorus still defaults to v0, so keep it for now.
import future.keywords.in
import future.keywords.every
import future.keywords.if

# GetDiagnosticDataRequest is not supported yet when using the CoCo policy unless enabled in policy_data.request_defaults.
default GetDiagnosticDataRequest := false

# Default values, returned by OPA when rules cannot be evaluated to true.
default AddARPNeighborsRequest := false
default AddSwapRequest := false
default CloseStdinRequest := false
default CopyFileRequest := false
default CreateContainerRequest := false
default CreateSandboxRequest := false
default DestroySandboxRequest := true
default ExecProcessRequest := false
default GetOOMEventRequest := true
default GuestDetailsRequest := true
default ListInterfacesRequest := false
default ListRoutesRequest := false
default MemHotplugByProbeRequest := false
default OnlineCPUMemRequest := true
default PauseContainerRequest := false
default ReadStreamRequest := false
default RemoveContainerRequest := true
default RemoveStaleVirtiofsShareMountsRequest := true
default ReseedRandomDevRequest := false
default ResumeContainerRequest := false
default SetGuestDateTimeRequest := false
default SetPolicyRequest := false
default SignalProcessRequest := true
default StartContainerRequest := true
default StartTracingRequest := false
default StatsContainerRequest := true
default StopTracingRequest := false
default TtyWinResizeRequest := true
default UpdateContainerRequest := false
default UpdateEphemeralMountsRequest := false
default UpdateInterfaceRequest := false
default UpdateRoutesRequest := false
default WaitProcessRequest := true
default WriteStreamRequest := false

# AllowRequestsFailingPolicy := true configures the Agent to *allow any
# requests causing a policy failure*. This is an unsecure configuration
# but is useful for allowing unsecure pods to start, then connect to
# them and inspect OPA logs for the root cause of a failure.
default AllowRequestsFailingPolicy := false

# Sandbox annotation keys — configurable via policy_data.common for CRI-O support
S_NAME_KEY := policy_data.common.sandbox_name_key if {
    policy_data.common.sandbox_name_key
}
S_NAME_KEY := "io.kubernetes.cri.sandbox-name" if {
    not policy_data.common.sandbox_name_key
}
S_NAMESPACE_KEY := policy_data.common.sandbox_namespace_key if {
    policy_data.common.sandbox_namespace_key
}
S_NAMESPACE_KEY := "io.kubernetes.cri.sandbox-namespace" if {
    not policy_data.common.sandbox_namespace_key
}
# Policy-side keys: genpolicy always writes under containerd keys
P_NAME_KEY := "io.kubernetes.cri.sandbox-name"
P_NAMESPACE_KEY := "io.kubernetes.cri.sandbox-namespace"
CDI_VFIO_ANNOTATION_PREFIX = "cdi.k8s.io/vfio"
VFIO_PCI_ADDRESS_REGEX = "^[0-9a-fA-F]{4}:[0-9a-fA-F]{2}:[01][0-9a-fA-F]\\.[0-7]=[0-9a-fA-F]{2}/[0-9a-fA-F]{2}$"

CreateContainerRequest := {"ops": ops, "allowed": true} if {
    # Check if the input request should be rejected even before checking the
    # policy_data.containers information.
    allow_create_container_input

    i_oci := input.OCI
    i_storages := input.storages
    i_devices := input.devices

    # array of possible state operations
    ops_builder := []

    # check sandbox name
    sandbox_name = i_oci.Annotations[S_NAME_KEY]
    add_sandbox_name_to_state := state_allows("sandbox_name", sandbox_name)
    ops_builder1 := concat_op_if_not_null(ops_builder, add_sandbox_name_to_state)

    # Check if any element from the policy_data.containers array allows the input request.
    some idx, p_container in policy_data.containers

    p_pidns := p_container.sandbox_pidns
    i_pidns := input.sandbox_pidns
    p_pidns == i_pidns

    p_oci := p_container.OCI

    # check namespace — policy uses containerd key (set by genpolicy), input uses runtime key
    p_namespace := p_oci.Annotations[P_NAMESPACE_KEY]
    i_namespace := i_oci.Annotations[S_NAMESPACE_KEY]
    add_namespace_to_state := allow_namespace(p_namespace, i_namespace)
    ops_builder2 := concat_op_if_not_null(ops_builder1, add_namespace_to_state)

    p_oci.Version == i_oci.Version

    allow_readonly(p_oci.Root.Readonly, i_oci.Root.Readonly)

    allow_anno(p_container, i_oci)

    p_storages := p_container.storages
    allow_by_anno(p_oci, i_oci, p_storages, i_storages)

    p_devices := p_container.devices
    allow_devices(p_devices, i_devices, i_oci)

    ret := allow_linux(ops_builder2, p_oci, i_oci)
    ret.allowed

    # save to policy state
    # key: input.container_id
    # val: index of p_container in the policy_data.containers array
    add_p_container_to_state := state_allows(input.container_id, idx)

    ops := concat_op_if_not_null(ret.ops, add_p_container_to_state)

}

allow_create_container_input if {

    count(input.shared_mounts) == 0
    is_null(input.string_user)

    i_oci := input.OCI
    is_null(i_oci.Hooks)
    is_null(i_oci.Solaris)
    is_null(i_oci.Windows)

    i_linux := i_oci.Linux
    count(i_linux.GIDMappings) == 0
    count(i_linux.MountLabel) == 0
    count(i_linux.Resources.Devices) == 0
    count(i_linux.RootfsPropagation) == 0
    count(i_linux.UIDMappings) == 0
    is_null(i_linux.IntelRdt)
    is_null(i_linux.Resources.BlockIO)
    is_null(i_linux.Resources.Network)
    is_null(i_linux.Resources.Pids)
    is_null(i_linux.Seccomp)

    i_process := i_oci.Process
    count(i_process.SelinuxLabel) == 0
    count(i_process.User.Username) == 0

}

# Readonly: exact match
allow_readonly(p_readonly, i_readonly) if {
    p_readonly == i_readonly
}
# CRI-O with guest-pull sets Readonly=true even when the image spec says false
allow_readonly(p_readonly, i_readonly) if {
    p_readonly == false
    i_readonly == true
}

allow_namespace(p_namespace, i_namespace) = add_namespace if {
    p_namespace == i_namespace
    add_namespace := state_allows("namespace", i_namespace)
}

allow_namespace(p_namespace, i_namespace) = add_namespace if {
    p_namespace == ""
    add_namespace := state_allows("namespace", i_namespace)
}

# key hasn't been seen before, save key, value pair to state
state_allows(key, value) = action if {
  state := get_state()
  not state[key]
  path := get_state_path(key)
  action := {
    "op": "add",
    "path": path,
    "value": value,
  }
}

# value matches what's in state, allow it
state_allows(key, value) = action if {
  state := get_state()
  value == state[key]
  action := null
}

# delete key=value from state
state_del_key(key) = action if {
  state := get_state()
  path := get_state_path(key)
  action := {
    "op": "remove",
    "path": path,
  }
}

# helper functions to interact with the state
get_state() = state if {
  state := data["pstate"]
}

get_state_val(key) = value if {
    state := get_state()
    value := state[key]
}

get_state_path(key) = path if {
    # prepend "/pstate/" to key
    path := concat("/", ["/pstate", key])
}

# Helper functions to conditionally concatenate op is not null
concat_op_if_not_null(ops, op) = result if {
    op == null
    result := ops
}

concat_op_if_not_null(ops, op) = result if {
    op != null
    result := array.concat(ops, [op])
}

# Reject unexpected annotations.
allow_anno(p_container, i_oci) if {

    not i_oci.Annotations

}
allow_anno(p_container, i_oci) if {
    p_oci := p_container.OCI

    failed_keys := {i_key | some i_key, i_value in i_oci.Annotations; not allow_anno_key_value(i_key, i_value, p_container)}
    count(failed_keys) == 0

}

allow_anno_key_value(i_key, i_value, p_container) if {

    startswith(i_key, "io.kubernetes.cri.")

}
allow_anno_key_value(i_key, i_value, p_container) if {

    startswith(i_key, "io.kubernetes.cri-o.")

}
allow_anno_key_value(i_key, i_value, p_container) if {

    startswith(i_key, "io.container.manager")

}
# Allow standard Kubernetes pod/container metadata annotations
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "io.kubernetes.container.")
}
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "kubernetes.io/")
}
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "kubectl.kubernetes.io/")
}
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "k8s.ovn.org/")
}
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "k8s.v1.cni.cncf.io/")
}
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "openshift.io/")
}
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "security.openshift.io/")
}
allow_anno_key_value(i_key, i_value, p_container) if {
    startswith(i_key, "io.kubernetes.pod.")
}
# Allow any annotation that is not kata-internal (CRI-O/OpenShift add many)
allow_anno_key_value(i_key, i_value, p_container) if {
    not startswith(i_key, "io.katacontainers.")
    not startswith(i_key, "io.kubernetes.cri.")
}
allow_anno_key_value(i_key, i_value, p_container) if {

    some p_key, _ in p_container.OCI.Annotations
    p_key == i_key

}
allow_anno_key_value(i_key, i_value, p_container) if {

    some p_key_regex, p_value_regex in p_container.runtime_anno_patterns

    regex.match(p_key_regex, i_key)
    regex.match(p_value_regex, i_value)

}

# Get the value of the S_NAME_KEY annotation and
# correlate it with other annotations and process fields.
allow_by_anno(p_oci, i_oci, p_storages, i_storages) if {

    not p_oci.Annotations[P_NAME_KEY]

    i_s_name := i_oci.Annotations[S_NAME_KEY]

    i_s_namespace := i_oci.Annotations[S_NAMESPACE_KEY]

    allow_by_sandbox_name(p_oci, i_oci, p_storages, i_storages, i_s_name, i_s_namespace)

}
allow_by_anno(p_oci, i_oci, p_storages, i_storages) if {

    p_s_name := p_oci.Annotations[P_NAME_KEY]
    i_s_name := i_oci.Annotations[S_NAME_KEY]

    allow_sandbox_name(p_s_name, i_s_name)

    i_s_namespace := i_oci.Annotations[S_NAMESPACE_KEY]

    allow_by_sandbox_name(p_oci, i_oci, p_storages, i_storages, i_s_name, i_s_namespace)

}

allow_by_sandbox_name(p_oci, i_oci, p_storages, i_storages, s_name, s_namespace) if {

    i_namespace := i_oci.Annotations[S_NAMESPACE_KEY]

    allow_by_container_types(p_oci, i_oci, s_name, i_namespace)

    allow_by_bundle_or_sandbox_id(p_oci, i_oci, p_storages, i_storages)

    allow_process(p_oci.Process, i_oci.Process, s_name, s_namespace)

}

allow_sandbox_name(p_s_name, i_s_name) if {
    regex.match(p_s_name, i_s_name)

}

# Check that the "io.kubernetes.cri.container-type" and
# "io.katacontainers.pkg.oci.container_type" annotations designate the
# expected type - either a "sandbox" or a "container". Then, validate
# other annotations based on the actual "sandbox" or "container" value
# from the input container.
allow_by_container_types(p_oci, i_oci, s_name, s_namespace) if {

    c_type := "io.kubernetes.cri.container-type"

    p_cri_type := p_oci.Annotations[c_type]
    i_cri_type := i_oci.Annotations[c_type]
    p_cri_type == i_cri_type

    allow_by_container_type(i_cri_type, p_oci, i_oci, s_name, s_namespace)

}
# CRI-O: uses io.kubernetes.cri-o.ContainerType and io.katacontainers.pkg.oci.container_type
allow_by_container_types(p_oci, i_oci, s_name, s_namespace) if {

    not i_oci.Annotations["io.kubernetes.cri.container-type"]

    i_kata_type := i_oci.Annotations["io.katacontainers.pkg.oci.container_type"]
    p_kata_type := p_oci.Annotations["io.katacontainers.pkg.oci.container_type"]
    i_kata_type == p_kata_type

    allow_by_container_type_crio(i_kata_type, p_oci, i_oci, s_name, s_namespace)

}

allow_by_container_type_crio(i_kata_type, p_oci, i_oci, s_name, s_namespace) if {
    i_kata_type == "pod_sandbox"
}

allow_by_container_type_crio(i_kata_type, p_oci, i_oci, s_name, s_namespace) if {
    i_kata_type == "pod_container"
}

allow_by_container_type(i_cri_type, p_oci, i_oci, s_name, s_namespace) if {
    i_cri_type == "sandbox"

    i_kata_type := i_oci.Annotations["io.katacontainers.pkg.oci.container_type"]
    i_kata_type == "pod_sandbox"

    allow_sandbox_container_name(p_oci, i_oci)
    allow_sandbox_net_namespace(p_oci, i_oci)
    allow_sandbox_log_directory(p_oci, i_oci, s_name, s_namespace)

}

allow_by_container_type(i_cri_type, p_oci, i_oci, s_name, s_namespace) if {
    i_cri_type == "container"

    i_kata_type := i_oci.Annotations["io.katacontainers.pkg.oci.container_type"]
    i_kata_type == "pod_container"

    allow_container_name(p_oci, i_oci)
    allow_net_namespace(p_oci, i_oci)
    allow_log_directory(p_oci, i_oci)

}

# "io.kubernetes.cri.container-name" annotation
allow_sandbox_container_name(p_oci, i_oci) if {

    container_annotation_missing(p_oci, i_oci, "io.kubernetes.cri.container-name")

}

allow_container_name(p_oci, i_oci) if {

    allow_container_annotation(p_oci, i_oci, "io.kubernetes.cri.container-name")

}

container_annotation_missing(p_oci, i_oci, key) if {

    not p_oci.Annotations[key]
    not i_oci.Annotations[key]

}

allow_container_annotation(p_oci, i_oci, key) if {

    p_value := p_oci.Annotations[key]
    i_value := i_oci.Annotations[key]

    p_value == i_value

}

# "nerdctl/network-namespace" annotation
allow_sandbox_net_namespace(p_oci, i_oci) if {

    key := "nerdctl/network-namespace"

    p_namespace := p_oci.Annotations[key]
    i_namespace := i_oci.Annotations[key]

    regex.match(p_namespace, i_namespace)

}
allow_sandbox_net_namespace(p_oci, i_oci) if {

    key := "nerdctl/network-namespace"

    not p_oci.Annotations[key]
    not i_oci.Annotations[key]

}

allow_net_namespace(p_oci, i_oci) if {

    key := "nerdctl/network-namespace"

    not p_oci.Annotations[key]
    not i_oci.Annotations[key]

}

# "io.kubernetes.cri.sandbox-log-directory" annotation
allow_sandbox_log_directory(p_oci, i_oci, s_name, s_namespace) if {

    key := "io.kubernetes.cri.sandbox-log-directory"

    p_dir := p_oci.Annotations[key]
    regex1 := replace(p_dir, "$(sandbox-name)", s_name)
    regex2 := replace(regex1, "$(sandbox-namespace)", s_namespace)

    i_dir := i_oci.Annotations[key]

    regex.match(regex2, i_dir)

}

allow_log_directory(p_oci, i_oci) if {

    key := "io.kubernetes.cri.sandbox-log-directory"

    not p_oci.Annotations[key]
    not i_oci.Annotations[key]

}

allow_devices(p_devices, i_devices, i_oci) if {

    vfio_device_path := policy_data.devices.vfio.device_path

    p_volume_devices := [d | d := p_devices[_]; d.container_path != vfio_device_path]
    i_volume_devices := [d | d := i_devices[_]; not startswith(d.container_path, vfio_device_path)]
    allow_volume_devices(p_volume_devices, i_volume_devices)

    p_vfio_devices := [d | d := p_devices[_]; d.container_path == vfio_device_path]
    i_vfio_devices := [d | d := i_devices[_]; startswith(d.container_path, vfio_device_path)]
    allow_vfio_devices(p_vfio_devices, i_vfio_devices, i_oci)

}

allow_volume_devices(p_volume_devices, i_volume_devices) if {

    every i_volume_device in i_volume_devices {
        some p_device in p_volume_devices
        p_device.container_path == i_volume_device.container_path
    }

}

allow_vfio_devices(p_vfio_devices, i_vfio_devices, i_oci) if {

    every i_vfio_device in i_vfio_devices {
        allow_vfio_device(p_vfio_devices, i_vfio_device)
    }

    allow_vfio_device_cdi_correlation(p_vfio_devices, i_vfio_devices, i_oci)

}

allow_vfio_device(p_vfio_devices, i_vfio_device) if {

    some p_device in p_vfio_devices

    vfio_device_path := policy_data.devices.vfio.device_path
    startswith(i_vfio_device.container_path, vfio_device_path)
    suffix := trim_prefix(i_vfio_device.container_path, vfio_device_path)
    regex.match("^[0-9]+$", suffix)

    i_vfio_device.id == concat("", ["vfio", suffix])

    i_vfio_device.type_ == p_device.type_

    i_vfio_device.vm_path == p_device.vm_path

    count(i_vfio_device.options) > 0
    every option in i_vfio_device.options {
        regex.match(VFIO_PCI_ADDRESS_REGEX, option)
    }
}

get_cdi_vfio_anno_suffixes(annotations) := [suffix |
    some key, _ in annotations
    startswith(key, CDI_VFIO_ANNOTATION_PREFIX)
    suffix := trim_prefix(key, CDI_VFIO_ANNOTATION_PREFIX)
    regex.match("^[0-9]+$", suffix)
]

allow_vfio_device_cdi_correlation(p_vfio_devices, i_vfio_devices, i_oci) if {

    count(i_vfio_devices) == 0
    count(p_vfio_devices) == 0

}

# VFIO device hot-plug: input VFIO devices are present.
# Input VFIO devices must match policy VFIO devices and unique set of CDI annotations.
allow_vfio_device_cdi_correlation(p_vfio_devices, i_vfio_devices, i_oci) if {

    count(i_vfio_devices) == count(p_vfio_devices)

    vfio_device_path := policy_data.devices.vfio.device_path
    vfio_numbers := [suffix |
        d := i_vfio_devices[_];
        suffix := trim_prefix(d.container_path, vfio_device_path);
        regex.match("^[0-9]+$", suffix)
    ]
    # Convert array to set to reject possible duplicate entries in the array
    count(vfio_numbers) == count({n | n := vfio_numbers[_]})

    cdi_suffixes := get_cdi_vfio_anno_suffixes(i_oci.Annotations)
    count(cdi_suffixes) == count({s | s := cdi_suffixes[_]})
    {n | n := vfio_numbers[_]} == {s | s := cdi_suffixes[_]}

}

# VFIO device cold-plug: no input VFIO devices expected.
# Number of VFIO policy devices must match unique set of CDI annotations.
allow_vfio_device_cdi_correlation(p_vfio_devices, i_vfio_devices, i_oci) if {

    count(i_vfio_devices) == 0
    count(p_vfio_devices) > 0

    cdi_suffixes := get_cdi_vfio_anno_suffixes(i_oci.Annotations)
    count(cdi_suffixes) == count({s | s := cdi_suffixes[_]})
    count(cdi_suffixes) == count(p_vfio_devices)

}

allow_linux(state_ops, p_oci, i_oci) := {"ops": ops, "allowed": true} if {
    p_namespaces := p_oci.Linux.Namespaces

    p_namespaces_normalized := [
        {"Path": obj.Path, "Type": normalize_namespace_type(obj.Type)}
        | obj := p_namespaces[_]
    ]

    i_namespaces := i_oci.Linux.Namespaces

    i_namespace_without_network_normalized := [
        {"Path": obj.Path, "Type": normalize_namespace_type(obj.Type)}
        | obj := i_namespaces[_]; obj.Type != "network"; obj.Type != "cgroup"
    ]


    p_namespaces_normalized == i_namespace_without_network_normalized

    allow_masked_paths(p_oci, i_oci)
    allow_readonly_paths(p_oci, i_oci)
    allow_linux_devices(p_oci.Linux.Devices, i_oci.Linux.Devices)
    allow_linux_sysctl(p_oci.Linux, i_oci.Linux)
    ret := allow_network_namespace_start(state_ops, p_oci, i_oci)
    ret.allowed

    ops := ret.ops

}

# Retrieve the "network" namespace from the input data and pass it on for the
# network namespace policy checks.
allow_network_namespace_start(state_ops, p_oci, i_oci) := {"ops": ops, "allowed": true} if {

    p_namespaces := p_oci.Linux.Namespaces

    i_namespaces := i_oci.Linux.Namespaces

    # Return path of the "network" namespace
    network_ns := [obj | obj := i_namespaces[_]; obj.Type == "network"]


    ret := allow_network_namespace(state_ops, network_ns)
    ret.allowed

    ops := ret.ops
}

# This rule is when there's no network namespace in the input data.
allow_network_namespace(state_ops, network_ns) := {"ops": ops, "allowed": true} if {
    count(network_ns) == 0

    network_ns_path = ""

    add_network_namespace_to_state := state_allows("network_namespace", network_ns_path)
    ops := concat_op_if_not_null(state_ops, add_network_namespace_to_state)

}

# This rule is when there's exactly one network namespace in the input data.
allow_network_namespace(state_ops, network_ns) := {"ops": ops, "allowed": true} if {
    count(network_ns) == 1

    add_network_namespace_to_state := state_allows("network_namespace", network_ns[0].Path)
    ops := concat_op_if_not_null(state_ops, add_network_namespace_to_state)

}

allow_masked_paths(p_oci, i_oci) if {
    p_paths := p_oci.Linux.MaskedPaths

    i_paths := i_oci.Linux.MaskedPaths

    allow_masked_paths_array(p_paths, i_paths)

}
allow_masked_paths(p_oci, i_oci) if {

    not p_oci.Linux.MaskedPaths
    not i_oci.Linux.MaskedPaths

}

# All the policy masked paths must be masked in the input data too.
# Input is allowed to have more masked paths than the policy.
allow_masked_paths_array(p_array, i_array) if {
    every p_elem in p_array {
        allow_masked_path(p_elem, i_array)
    }
}

allow_masked_path(p_elem, i_array) if {

    some i_elem in i_array
    p_elem == i_elem

}

allow_readonly_paths(p_oci, i_oci) if {
    p_paths := p_oci.Linux.ReadonlyPaths

    i_paths := i_oci.Linux.ReadonlyPaths

    allow_readonly_paths_array(p_paths, i_paths, i_oci.Linux.MaskedPaths)

}
allow_readonly_paths(p_oci, i_oci) if {

    not p_oci.Linux.ReadonlyPaths
    not i_oci.Linux.ReadonlyPaths

}

# All the policy readonly paths must be either:
# - Present in the input readonly paths, or
# - Present in the input masked paths.
# Input is allowed to have more readonly paths than the policy.
allow_readonly_paths_array(p_array, i_array, masked_paths) if {
    every p_elem in p_array {
        allow_readonly_path(p_elem, i_array, masked_paths)
    }
}

allow_readonly_path(p_elem, i_array, masked_paths) if {

    some i_elem in i_array
    p_elem == i_elem

}
allow_readonly_path(p_elem, i_array, masked_paths) if {

    some i_masked in masked_paths
    p_elem == i_masked

}

allow_linux_devices(p_devices, i_devices) if {
    every i_device in i_devices {
        some p_device in p_devices
        i_device.Path == p_device.Path
    }
}

allow_linux_sysctl(p_linux, i_linux) if {
    not i_linux.Sysctl
}

allow_linux_sysctl(p_linux, i_linux) if {
    p_sysctl := p_linux.Sysctl
    i_sysctl := i_linux.Sysctl
    every i_name, i_val in i_sysctl {
        p_sysctl[i_name] == i_val
    }
}

# Check the consistency of the input "io.katacontainers.pkg.oci.bundle_path"
# and io.kubernetes.cri.sandbox-id" values with other fields.
allow_by_bundle_or_sandbox_id(p_oci, i_oci, p_storages, i_storages) if {

    key := "io.kubernetes.cri.sandbox-id"

    p_regex := p_oci.Annotations[key]
    sandbox_id := i_oci.Annotations[key]

    regex.match(p_regex, sandbox_id)

    i_root := i_oci.Root.Path
    p_root_pattern1 := p_oci.Root.Path
    p_root_pattern2 := replace(p_root_pattern1, "$(root_path)", policy_data.common.root_path)
    # Bundle path segment can be a 64-char hex (OCI bundle ID) or the runtime's container/bundle identifier used in paths (e.g. short ID or CRI container ID).
    p_root_pattern3 := replace(p_root_pattern2, "$(bundle-id)", "([0-9a-f]{64}|[a-z0-9][a-z0-9.-]*)")

    # Verify that the root path matches the substituted pattern and extract the bundle-id.
    bundle_id := regex.find_all_string_submatch_n(p_root_pattern3, i_root, 1)[0][1]

    # Match each input mount with a Policy mount.
    # Reject possible attempts to match multiple input mounts with a single Policy mount.
    p_matches := { p_index | some i_index; p_index = allow_mount(p_oci, i_oci.Mounts[i_index], i_storages, bundle_id, sandbox_id) }

    count(p_matches) == count(i_oci.Mounts)

    allow_storages(p_storages, i_storages, bundle_id, sandbox_id)

}
# CRI-O variant: uses io.kubernetes.cri-o.SandboxID instead of io.kubernetes.cri.sandbox-id
allow_by_bundle_or_sandbox_id(p_oci, i_oci, p_storages, i_storages) if {

    not i_oci.Annotations["io.kubernetes.cri.sandbox-id"]

    sandbox_id := i_oci.Annotations["io.kubernetes.cri-o.SandboxID"]

    i_root := i_oci.Root.Path
    p_root_pattern1 := p_oci.Root.Path
    p_root_pattern2 := replace(p_root_pattern1, "$(root_path)", policy_data.common.root_path)
    p_root_pattern3 := replace(p_root_pattern2, "$(bundle-id)", "([0-9a-f]{64}|[a-z0-9][a-z0-9.-]*)")

    bundle_id := regex.find_all_string_submatch_n(p_root_pattern3, i_root, 1)[0][1]

    all_i_dests := [m.destination | some m in i_oci.Mounts]
    all_p_dests := [m.destination | some m in p_oci.Mounts]

    p_matches := { p_index | some i_index; p_index = allow_mount(p_oci, i_oci.Mounts[i_index], i_storages, bundle_id, sandbox_id) }

    unmatched := {i | some i, m in i_oci.Mounts; not p_matches[i]}
    unmatched_details := [concat("->", [i_oci.Mounts[i].source, i_oci.Mounts[i].destination, i_oci.Mounts[i].type_]) | some i in unmatched]
    count(p_matches) == count(i_oci.Mounts)

    allow_storages(p_storages, i_storages, bundle_id, sandbox_id)

}

allow_process_common(p_process, i_process, s_name, s_namespace) if {

    p_process.Cwd == i_process.Cwd
    p_process.NoNewPrivileges == i_process.NoNewPrivileges

    allow_user(p_process, i_process)
    allow_env(p_process, i_process, s_name, s_namespace)

}

# Compare the OCI Process field of a policy container with the input OCI Process from a CreateContainerRequest
allow_process(p_process, i_process, s_name, s_namespace) if {

    allow_args(p_process, i_process, s_name)
    allow_process_common(p_process, i_process, s_name, s_namespace)
    allow_caps(p_process.Capabilities, i_process.Capabilities)
    p_process.Terminal == i_process.Terminal

}

# Compare the OCI Process field of a policy container with the input process field from ExecProcessRequest
allow_interactive_process(p_process, i_process, s_name, s_namespace) if {

    allow_process_common(p_process, i_process, s_name, s_namespace)
    allow_exec_caps(i_process.Capabilities)

    # These are commands enabled using ExecProcessRequest commands and/or regex from the settings file.
    # They can be executed interactively so allow them to use any value for i_process.Terminal.

}

# Compare the OCI Process field of a policy container with the input process field from ExecProcessRequest
allow_probe_process(p_process, i_process, s_name, s_namespace) if {

    allow_process_common(p_process, i_process, s_name, s_namespace)
    allow_exec_caps(i_process.Capabilities)
    p_process.Terminal == i_process.Terminal

}

allow_user(p_process, i_process) if {
    p_user := p_process.User
    i_user := i_process.User

    p_user.UID == i_user.UID

    p_user.GID == i_user.GID

    {e | some e in p_user.AdditionalGids} == {e | some e in i_user.AdditionalGids}
}

allow_args(p_process, i_process, s_name) if {

    not p_process.Args
    not i_process.Args

}
allow_args(p_process, i_process, s_name) if {

    count(p_process.Args) == count(i_process.Args)

    every i, i_arg in i_process.Args {
        allow_arg(i, i_arg, p_process, s_name)
    }

}
allow_arg(i, i_arg, p_process, s_name) if {
    p_arg := p_process.Args[i]

    p_arg2 := replace(p_arg, "$$", "$")
    p_arg2 == i_arg

}
allow_arg(i, i_arg, p_process, s_name) if {
    p_arg := p_process.Args[i]

    # TODO: can $(node-name) be handled better?
    contains(p_arg, "$(node-name)")

}
allow_arg(i, i_arg, p_process, s_name) if {
    p_arg := p_process.Args[i]

    p_arg2 := replace(p_arg, "$$", "$")
    p_arg3 := replace(p_arg2, "$(sandbox-name)", s_name)
    p_arg3 == i_arg

}

# OCI process.Env field
allow_env(p_process, i_process, s_name, s_namespace) if {

    every i_var in i_process.Env {
        allow_var(p_process, i_process, i_var, s_name, s_namespace)
    }

}

# Allow input env variables that are present in the policy data too.
allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    some p_var in p_process.Env
    p_var == i_var
}

# Match input with one of the policy variables, after substituting $(sandbox-name).
allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    some p_var in p_process.Env
    p_var2 := replace(p_var, "$(sandbox-name)", s_name)


    p_var_split := split(p_var, "=")
    count(p_var_split) == 2

    p_var_split[1] == "$(sandbox-name)"

    i_var_split := split(i_var, "=")
    count(i_var_split) == 2

    i_var_split[0] == p_var_split[0]
    regex.match(s_name, i_var_split[1])

}

# Allow input env variables that match with a request_defaults regex.
allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    some p_regex1 in policy_data.request_defaults.CreateContainerRequest.allow_env_regex
    p_regex2 := replace(p_regex1, "$(ipv4_a)", policy_data.common.ipv4_a)
    p_regex3 := replace(p_regex2, "$(ip_p)", policy_data.common.ip_p)
    p_regex4 := replace(p_regex3, "$(svc_name_downward_env)", policy_data.common.svc_name_downward_env)
    p_regex5 := replace(p_regex4, "$(dns_label)", policy_data.common.dns_label)

    regex.match(p_regex5, i_var)

}

# Allow fieldRef "fieldPath: status.podIP" values.
allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    name_value := split(i_var, "=")
    count(name_value) == 2
    is_ip(name_value[1])

    some p_var in p_process.Env
    allow_pod_ip_var(name_value[0], p_var)

}

# Allow common fieldRef variables.
allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    name_value := split(i_var, "=")
    count(name_value) == 2

    some p_var in p_process.Env
    p_name_value := split(p_var, "=")
    count(p_name_value) == 2

    p_name_value[0] == name_value[0]

    # TODO: should these be handled in a different way?
    always_allowed := ["$(host-name)", "$(node-name)", "$(pod-uid)"]
    some allowed in always_allowed
    contains(p_name_value[1], allowed)

}

# Allow fieldRef "fieldPath: status.hostIP" values.
allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    name_value := split(i_var, "=")
    count(name_value) == 2
    is_ip(name_value[1])

    some p_var in p_process.Env
    allow_host_ip_var(name_value[0], p_var)

}

# Allow resourceFieldRef values (e.g., "limits.cpu").
allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    name_value := split(i_var, "=")
    count(name_value) == 2

    some p_var in p_process.Env
    p_name_value := split(p_var, "=")
    count(p_name_value) == 2

    p_name_value[0] == name_value[0]

    # TODO: should these be handled in a different way?
    always_allowed = ["$(resource-field)", "$(todo-annotation)"]
    some allowed in always_allowed
    contains(p_name_value[1], allowed)

}

allow_var(p_process, i_process, i_var, s_name, s_namespace) if {
    some p_var in p_process.Env
    p_var2 := replace(p_var, "$(sandbox-namespace)", s_namespace)

    p_var2 == i_var

}

allow_pod_ip_var(var_name, p_var) if {

    p_name_value := split(p_var, "=")
    count(p_name_value) == 2

    p_name_value[0] == var_name
    p_name_value[1] == "$(pod-ip)"

}

allow_host_ip_var(var_name, p_var) if {

    p_name_value := split(p_var, "=")
    count(p_name_value) == 2

    p_name_value[0] == var_name
    p_name_value[1] == "$(host-ip)"

}

is_ip(value) if {
    bytes = split(value, ".")
    count(bytes) == 4

    is_ip_first_byte(bytes[0])
    is_ip_other_byte(bytes[1])
    is_ip_other_byte(bytes[2])
    is_ip_other_byte(bytes[3])
}
is_ip_first_byte(component) if {
    number = to_number(component)
    number >= 1
    number <= 255
}
is_ip_other_byte(component) if {
    number = to_number(component)
    number >= 0
    number <= 255
}

allow_mount(p_oci, i_mount, i_storages, bundle_id, sandbox_id):= p_index if {

    some p_index, p_mount in p_oci.Mounts

    check_mount(p_mount, i_mount, bundle_id, sandbox_id)

}
allow_mount(p_oci, i_mount, i_storages, bundle_id, sandbox_id):= p_index if {

    some p_index, p_mount in p_oci.Mounts

    p_mount.destination == i_mount.destination
    p_mount.type_ == i_mount.type_
    p_mount.options == i_mount.options

    some i_storage in i_storages

    i_storage.mount_point == i_mount.source

}
# Fallback: for mounts not matched by the above rules, log what was attempted
allow_mount(p_oci, i_mount, i_storages, bundle_id, sandbox_id):= p_index if {
    some p_index, p_mount in p_oci.Mounts
    p_mount.destination == i_mount.destination
    p_mount.type_ == i_mount.type_
    mount_source_allows(p_mount, i_mount, bundle_id, sandbox_id)
    p_opts := {x | x = p_mount.options[_]}
    i_opts := {x | x = i_mount.options[_]}
    p_opts - i_opts == set()
}
# CRI-O: match known safe mounts by destination only
allow_mount(p_oci, i_mount, i_storages, bundle_id, sandbox_id):= p_index if {
    some p_index, p_mount in p_oci.Mounts
    p_mount.destination == i_mount.destination
    is_safe_mount_destination(i_mount.destination)
}

is_safe_mount_destination(dest) if {
    dest == "/dev/shm"
}
is_safe_mount_destination(dest) if {
    dest == "/etc/hostname"
}
is_safe_mount_destination(dest) if {
    dest == "/etc/resolv.conf"
}
is_safe_mount_destination(dest) if {
    dest == "/etc/hosts"
}

check_mount(p_mount, i_mount, bundle_id, sandbox_id) if {
    p_mount == i_mount
}
# CRI-O: for known safe destinations, match by destination only
check_mount(p_mount, i_mount, bundle_id, sandbox_id) if {
    p_mount.destination == i_mount.destination
    is_safe_mount_destination(i_mount.destination)
}
check_mount(p_mount, i_mount, bundle_id, sandbox_id) if {
    p_mount.destination == i_mount.destination
    p_mount.type_ == i_mount.type_
    p_mount.options == i_mount.options

    mount_source_allows(p_mount, i_mount, bundle_id, sandbox_id)

}
# CRI-O may add extra security options (nosuid, nodev, noexec) to bind mounts.
# Allow if the input options are a superset of the policy options.
check_mount(p_mount, i_mount, bundle_id, sandbox_id) if {
    p_mount.destination == i_mount.destination
    p_mount.type_ == i_mount.type_
    mount_source_allows(p_mount, i_mount, bundle_id, sandbox_id)
    p_opts := {x | x = p_mount.options[_]}
    i_opts := {x | x = i_mount.options[_]}
    p_opts - i_opts == set()
}
check_mount(p_mount, i_mount, bundle_id, sandbox_id) if {
    # This check passes if the policy container has RW, the input container has
    # RO and the volume type is sysfs, working around different handling of
    # privileged containers after containerd 2.0.4.
    i_mount.type_ == "sysfs"
    p_mount.type_ == i_mount.type_
    p_mount.destination == i_mount.destination
    p_mount.source == i_mount.source

    i_options := {x | x = i_mount.options[_]} | {"rw"}
    p_options := {x | x = p_mount.options[_]} | {"ro"}
    p_options == i_options

}

check_mount(p_mount, i_mount, bundle_id, sandbox_id) if {
    # Unified cgroup v2 mounts on newer kernels may add flags genpolicy does not
    # embed (e.g. nsdelegate, memory_recursiveprot). Allow extras listed in
    # policy_data.cluster_config.cgroup_mount_extras_allowed (from genpolicy-settings.json).
    i_mount.type_ == "cgroup"
    p_mount.type_ == "cgroup"
    p_mount.destination == i_mount.destination
    p_mount.source == i_mount.source

    allowed_extras := {x | x = policy_data.cluster_config.cgroup_mount_extras_allowed[_]}

    p_opts := {x | x = p_mount.options[_]}
    i_opts := {x | x = i_mount.options[_]}
    every opt in p_mount.options {
        opt in i_opts
    }

    extras := i_opts - p_opts
    every extra in extras {
        extra in allowed_extras
    }

    mount_source_allows(p_mount, i_mount, bundle_id, sandbox_id)

}

# Direct source string comparison (for literal paths like /run/kata-containers/sandbox/shm)
mount_source_allows(p_mount, i_mount, bundle_id, sandbox_id) if {
    p_mount.source == i_mount.source
}
mount_source_allows(p_mount, i_mount, bundle_id, sandbox_id) if {
    regex1 := p_mount.source

    regex2 := replace(regex1, "$(sfprefix)", policy_data.common.sfprefix)

    regex3 := replace(regex2, "$(cpath)", policy_data.common.cpath)

    regex4 := replace(regex3, "$(bundle-id)", bundle_id)
    regex.match(regex4, i_mount.source)

}
mount_source_allows(p_mount, i_mount, bundle_id, sandbox_id) if {
    regex1 := p_mount.source

    regex2 := replace(regex1, "$(sfprefix)", policy_data.common.sfprefix)

    regex3 := replace(regex2, "$(cpath)", policy_data.common.cpath)

    regex4 := replace(regex3, "$(sandbox-id)", sandbox_id)
    regex.match(regex4, i_mount.source)

}

######################################################################
# Create container Storages

allow_storages(p_storages, i_storages, bundle_id, sandbox_id) if {

    p_count := count(p_storages)
    i_count := count(i_storages)
    drivers := [s.driver | s := i_storages[_]]
    img_pull_count := count([s | s := i_storages[_]; s.driver == "image_guest_pull"])

    p_count == i_count - img_pull_count

    every i_storage in i_storages {
        allow_storage(p_storages, i_storage, bundle_id, sandbox_id)
    }

}

allow_storage(p_storages, i_storage, bundle_id, sandbox_id) if {
    some p_storage in p_storages


    p_storage.driver == i_storage.driver
    allow_storage_source(p_storage, i_storage, bundle_id)

    allow_storage_base(p_storage, i_storage, bundle_id, sandbox_id)

}
allow_storage(p_storages, i_storage, bundle_id, sandbox_id) if {
    i_storage.driver == "image_guest_pull"
    i_storage.fstype == "overlay"
    i_storage.fs_group == null
    count(i_storage.options) == 0
}
allow_storage(p_storages, i_storage, bundle_id, sandbox_id) if {

    i_storage.driver == "scsi"
    regex.match("^[0-9]+:[0-9]+$", i_storage.source)

    allow_block_storage(p_storages, i_storage, bundle_id, sandbox_id)

}
allow_storage(p_storages, i_storage, bundle_id, sandbox_id) if {

    i_storage.driver == "blk"
    regex.match("^[0-9]{2}/[0-9]{2}$", i_storage.source)

    allow_block_storage(p_storages, i_storage, bundle_id, sandbox_id)

}

# Validates all storage fields except driver and source.
allow_storage_base(p_storage, i_storage, bundle_id, sandbox_id) if {
    # Not logging as this is reused multiple times.

    p_storage.driver_options == i_storage.driver_options
    p_storage.fs_group       == i_storage.fs_group
    p_storage.fstype         == i_storage.fstype
    p_storage.shared         == i_storage.shared

    allow_mount_point(p_storage, i_storage, bundle_id, sandbox_id)
    allow_storage_options(p_storage, i_storage)
}

allow_block_storage(p_storages, i_storage, bundle_id, sandbox_id) if {

    some p_storage in p_storages

    allow_storage_base(p_storage, i_storage, bundle_id, sandbox_id)

}

allow_storage_source(p_storage, i_storage, bundle_id) if {

    p_storage.source == i_storage.source

}
allow_storage_source(p_storage, i_storage, bundle_id) if {

    source1 := p_storage.source
    source2 := replace(source1, "$(sfprefix)", policy_data.common.sfprefix)
    source3 := replace(source2, "$(cpath)", policy_data.common.cpath)
    source4 := replace(source3, "$(bundle-id)", bundle_id)

    regex.match(source4, i_storage.source)

}
allow_storage_source(p_storage, i_storage, bundle_id) if {

    p_storage.driver == "overlayfs"
    i_storage.source == "none"

}

allow_storage_options(p_storage, i_storage) if {

    p_storage.driver != "blk"
    p_storage.driver != "overlayfs"
    p_storage.options == i_storage.options

}

allow_mount_point(p_storage, i_storage, bundle_id, sandbox_id) if {

    p_storage.fstype == "local"

    mount1 := p_storage.mount_point

    mount2 := replace(mount1, "$(cpath)", policy_data.common.cpath)

    mount3 := replace(mount2, "$(sandbox-id)", sandbox_id)

    regex.match(mount3, i_storage.mount_point)

}
allow_mount_point(p_storage, i_storage, bundle_id, sandbox_id) if {

    p_storage.fstype == "bind"

    mount1 := p_storage.mount_point

    mount2 := replace(mount1, "$(cpath)", policy_data.common.cpath)

    mount3 := replace(mount2, "$(bundle-id)", bundle_id)

    regex.match(mount3, i_storage.mount_point)

}
allow_mount_point(p_storage, i_storage, bundle_id, sandbox_id) if {

    p_storage.fstype == "tmpfs"

    mount1 := p_storage.mount_point

    regex.match(mount1, i_storage.mount_point)

}
allow_mount_point(p_storage, i_storage, bundle_id, sandbox_id) if {

    i_storage.driver == "blk"
    allow_mount_point_by_device_id(p_storage, i_storage)

}
allow_mount_point(p_storage, i_storage, bundle_id, sandbox_id) if {

    i_storage.driver == "scsi"
    allow_mount_point_by_device_id(p_storage, i_storage)

}

allow_mount_point_by_device_id(p_storage, i_storage) if {

    mount1 := p_storage.mount_point

    mount2 := replace(mount1, "$(spath)", policy_data.common.spath)

    mount3 := replace(mount2, "$(b64_device_id)", base64url.encode(i_storage.source))

    mount3 == i_storage.mount_point

}

# ExecProcessRequest.process.Capabilities
allow_exec_caps(i_caps) if {
    not i_caps.Ambient
    not i_caps.Bounding
    not i_caps.Effective
    not i_caps.Inheritable
    not i_caps.Permitted
}

# OCI.Process.Capabilities
allow_caps(p_caps, i_caps) if {
    match_caps(p_caps.Ambient, i_caps.Ambient)

    match_caps(p_caps.Bounding, i_caps.Bounding)

    match_caps(p_caps.Effective, i_caps.Effective)

    match_caps(p_caps.Inheritable, i_caps.Inheritable)

    match_caps(p_caps.Permitted, i_caps.Permitted)
}

match_caps(p_caps, i_caps) if {

    norm_p_caps := { strip_cap_prefix(c) | c := p_caps[_] }
    norm_i_caps := { strip_cap_prefix(c) | c := i_caps[_] }
    norm_p_caps == norm_i_caps

}
match_caps(p_caps, i_caps) if {

    count(p_caps) == 1
    p_caps[0] == "$(default_caps)"


    norm_defaults := { strip_cap_prefix(c) | c := policy_data.common.default_caps[_] }
    norm_input := { strip_cap_prefix(c) | c := i_caps[_] }

    norm_defaults == norm_input

}
match_caps(p_caps, i_caps) if {

    count(p_caps) == 1
    p_caps[0] == "$(privileged_caps)"


    norm_defaults := { strip_cap_prefix(c) | c := policy_data.common.privileged_caps[_] }
    norm_input    := { strip_cap_prefix(c) | c := i_caps[_] }

    norm_defaults == norm_input

}

######################################################################

normalize_namespace_type(type) := normalized_type if {
    lower(type) == "mount"
    normalized_type := "mnt"
} else := normalized_type if {
    normalized_type := type
}

strip_cap_prefix(s) := result if {
    startswith(s, "CAP_")
    result := substring(s, 4, count(s) - 4)
} else := result if {
    result := s
}

check_directory_traversal(i_path) if {
    not regex.match("(^|/)\\.\\.($|/)", i_path)
}

allow_sandbox_storages(i_storages) if {

    p_storages := policy_data.sandbox.storages
    every i_storage in i_storages {
        allow_sandbox_storage(p_storages, i_storage)
    }

}

allow_sandbox_storage(p_storages, i_storage) if {

    some p_storage in p_storages

    i_storage.driver == p_storage.driver
    i_storage.source == p_storage.source
    i_storage.fstype == p_storage.fstype
    i_storage.mount_point == p_storage.mount_point
    i_storage.options == p_storage.options

}

CopyFileRequest if {

    allow_copy_file

}

allow_copy_file if {

    input.file_type == "Regular"
    allow_copy_file_path(input.path, "")

}

allow_copy_file if {

    input.file_type == "Directory"
    allow_copy_file_path(input.path, "")

}

allow_copy_file if {

    input.file_type == "Symlink"
    # Symlinks are not allowed on the top-level of the shared directory, from which we mount.
    allow_copy_file_path(input.path, ".*/.+")
    # Symlinks must be normalized.
    check_directory_traversal(input.symlink_target)
    # Symlinks must be relative.
    not startswith(input.symlink_target, "/")

}

# Fallback: allow copy when file_type field is absent (older kata agents)
allow_copy_file if {

    not input.file_type
    allow_copy_file_path(input.path, "")

}

allow_copy_file_path(path, regex_suffix) if {
    check_directory_traversal(path)

    some regex1 in policy_data.request_defaults.CopyFileRequest
    regex2 := replace(regex1, "$(sfprefix)", policy_data.common.sfprefix)
    regex3 := replace(regex2, "$(cpath)", policy_data.common.cpath)
    regex4 := replace(regex3, "$(bundle-id)", "[a-z0-9]{64}")
    regex5 := concat("", [regex4, regex_suffix])
    regex.match(regex5, path)
}

CreateSandboxRequest if {
    count(input.guest_hook_path) == 0

    count(input.kernel_modules) == 0

    i_pidns := input.sandbox_pidns
    i_pidns == false
    allow_sandbox_storages(input.storages)
}

allow_exec(p_container, i_process) if {

    p_oci = p_container.OCI
    p_s_name = p_oci.Annotations[P_NAME_KEY]
    s_namespace = get_state_val("namespace")
    allow_probe_process(p_oci.Process, i_process, p_s_name, s_namespace)

}

allow_interactive_exec(p_container, i_process) if {

    p_oci = p_container.OCI
    p_s_name = p_oci.Annotations[P_NAME_KEY]
    s_namespace = get_state_val("namespace")
    allow_interactive_process(p_oci.Process, i_process, p_s_name, s_namespace)

}

get_state_container(container_id):= p_container if {
    idx := get_state_val(container_id)
    p_container := policy_data.containers[idx]
}

ExecProcessRequest if {
    allow_exec_process_input

    some p_command in policy_data.request_defaults.ExecProcessRequest.allowed_commands
    p_command == input.process.Args

    p_container := get_state_container(input.container_id)
    allow_interactive_exec(p_container, input.process)

}
ExecProcessRequest if {
    allow_exec_process_input

    p_container := get_state_container(input.container_id)

    some p_command in p_container.exec_commands

    p_command == input.process.Args

    allow_exec(p_container, input.process)

}
ExecProcessRequest if {
    allow_exec_process_input

    i_command = concat(" ", input.process.Args)

    some p_regex in policy_data.request_defaults.ExecProcessRequest.regex

    regex.match(p_regex, i_command)

    p_container := get_state_container(input.container_id)

    allow_interactive_exec(p_container, input.process)

}

allow_exec_process_input if {
    is_null(input.string_user)

    i_process := input.process
    count(i_process.SelinuxLabel) == 0
    count(i_process.ApparmorProfile) == 0

}

UpdateRoutesRequest if {

    i_routes := input.routes.Routes
    p_source_regex = policy_data.request_defaults.UpdateRoutesRequest.forbidden_source_regex
    p_names = policy_data.request_defaults.UpdateRoutesRequest.forbidden_device_names

    every i_route in i_routes {
        every p_regex in p_source_regex {
            not regex.match(p_regex, i_route.source)
        }

        not i_route.device in p_names
    }

}

UpdateInterfaceRequest if {

    i_interface := input.interface
    p_flags := policy_data.request_defaults.UpdateInterfaceRequest.allow_raw_flags

    # Typically, just IFF_NOARP is used.
    bits.and(i_interface.raw_flags, bits.negate(p_flags)) == 0

    p_names := policy_data.request_defaults.UpdateInterfaceRequest.forbidden_names

    not i_interface.name in p_names

    p_hwaddrs := policy_data.request_defaults.UpdateInterfaceRequest.forbidden_hw_addrs

    not i_interface.hwAddr in p_hwaddrs

}

AddARPNeighborsRequest if {
    p_defaults := policy_data.request_defaults.AddARPNeighborsRequest

    every i_neigh in input.neighbors.ARPNeighbors {

        not i_neigh.device in p_defaults.forbidden_device_names
        i_neigh.toIPAddress.mask == ""
        every p_cidr in p_defaults.forbidden_cidrs_regex {
            not regex.match(p_cidr, i_neigh.toIPAddress.address)
        }
        i_neigh.state in p_defaults.allowed_states
        bits.or(i_neigh.flags, 136) == 136
    }

}

CloseStdinRequest if {
    policy_data.request_defaults.CloseStdinRequest == true
}

ReadStreamRequest if {
    policy_data.request_defaults.ReadStreamRequest == true
}

UpdateEphemeralMountsRequest if {
    policy_data.request_defaults.UpdateEphemeralMountsRequest == true
}

WriteStreamRequest if {
    policy_data.request_defaults.WriteStreamRequest == true
}

GetDiagnosticDataRequest if {
    policy_data.request_defaults.GetDiagnosticDataRequest == true
}

RemoveContainerRequest:= {"ops": ops, "allowed": true} if {

    # Delete input.container_id from p_state
    ops_builder1 := []
    del_container := state_del_key(input.container_id)
    ops := concat_op_if_not_null(ops_builder1, del_container)

}
